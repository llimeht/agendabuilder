; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "AgendaBuilder"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "(c) 2021 Stuart Prescott"
#define MyAppURL "http://www.complexfluids.net/"
#define MyAppExeName "agendabuilder.exe"
#define MyProgId "AgendaBuilder.yaml"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C38413E9-1865-458B-9D62-8FBA72F0FF4C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}-{#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
DisableDirPage=no
UsedUserAreasWarning=no
LicenseFile=license.txt
ArchitecturesInstallIn64BitMode=x64
OutputBaseFilename=setupAgendaBuilder
SetupIconFile=dist\agendabuilder\icons\agendabuilder.ico
ChangesAssociations=yes

; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; \
    MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; \
    Description: "{cm:CreateDesktopIcon}"; \
    GroupDescription: "{cm:AdditionalIcons}"; \
    Flags: unchecked

[Files]
Source: "dist\agendabuilder\agendabuilder.exe"; \
    DestDir: "{app}"; Flags: ignoreversion
Source: "dist\agendabuilder\*"; \
    DestDir: "{app}"; \
    Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
; Start Menu icons
; Name: "{group}\AgendaBuilder"; \
;     Filename: "{app}\agendabuilder.exe"; \
;     WorkingDir: "{app}"; \
;     IconFilename: "{app}\icons\agendabuilder.ico"
Name: "{group}\AgendaBuilder make agenda"; \
    Filename: "{app}\agendabuilder.exe"; \
    Parameters: "agenda"; \
    WorkingDir: "{app}"; \
    IconFilename: "{app}\icons\agendabuilder.ico"
Name: "{group}\AgendaBuilder make meeting pack"; \
    Filename: "{app}\agendabuilder.exe"; \
    Parameters: "pack"; \
    WorkingDir: "{app}"; \
    IconFilename: "{app}\icons\agendabuilder.ico"
; Desktop Icons
; Name: "{autodesktop}\{#MyAppName}"; \
;     Filename: "{app}\{#MyAppExeName}"; \
;     IconFilename: "{app}\icons\agendabuilder.ico"; \
;     Tasks: desktopicon
Name: "{autodesktop}\AgendaBuilder make agenda"; \
    Filename: "{app}\agendabuilder.exe"; \
    Parameters: "agenda"; \
    WorkingDir: "{app}"; \
    IconFilename: "{app}\icons\agendabuilder.ico"; \
    Tasks: desktopicon
Name: "{autodesktop}\AgendaBuilder make meeting pack"; \
    Filename: "{app}\agendabuilder.exe"; \
    Parameters: "pack"; \
    WorkingDir: "{app}"; \
    IconFilename: "{app}\icons\agendabuilder.ico"; \
    Tasks: desktopicon
; Uninstall icon
Name: "{group}\{cm:UninstallProgram, AgendaBuilder}"; \
    Filename: "{uninstallexe}"

[UninstallDelete]
Type: dirifempty; \
    Name: "{app}"

[Registry]
; File types
Root: HKA; \
    Subkey: "Software\Classes\.yaml\OpenWithProgids"; \
    ValueType: string; \
    ValueName: "{#MyProgId}"; \
    ValueData: ""; \
    Flags: uninsdeletevalue
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}"; \
    ValueType: string; \
    ValueName: ""; \
    ValueData: "AgendaBuilder Meeting Agenda"; \
    Flags: uninsdeletekey
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}\DefaultIcon"; \
    ValueType: string; \
    ValueName: ""; \
    ValueData: "{app}\icons\agendabuilder.ico"
; Default open action
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}\shell\open\command"; \
    ValueType: string; \
    ValueName: ""; \
    ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; \
    Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; \
    ValueType: string; \
    ValueName: ".yaml"; \
    ValueData: ""
; Context menu actions
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}\shell\Build meeting agenda"; \
    Flags: uninsdeletekey
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}\shell\Build meeting agenda\command"; \
    ValueType: string; \
    ValueName: ""; \
    ValueData: """{app}\{#MyAppExeName}"" agenda ""%1"""
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}\shell\Build meeting pack"; \
    Flags: uninsdeletekey
Root: HKA; \
    Subkey: "Software\Classes\{#MyProgId}\shell\Build meeting pack\command"; \
    ValueType: string; \
    ValueName: ""; \
    ValueData: """{app}\{#MyAppExeName}"" pack ""%1"""
